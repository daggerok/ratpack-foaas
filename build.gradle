buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/repo" }
    jcenter()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:0.9.0-SNAPSHOT'
  }
}

repositories {
  maven { url "http://oss.jfrog.org/repo" }
  jcenter()
  maven { url "http://repo.springsource.org/repo" } // for springloaded
}

apply plugin: "ratpack-groovy"

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
  authors = [
    'Dan Woods',
    'Luke Daley',
    'Eric Ridge',
    'Russell Hart'
  ]
}

println "Ratpack FOAAS - By: ${authors.join(', ')}"

dependencies {
  springloaded "org.springsource.springloaded:springloaded-core:1.1.1"
  compile "io.ratpack:ratpack-jackson:0.9.0-SNAPSHOT"
  compile "com.itextpdf:itextpdf:5.4.2"
  runtime 'org.bouncycastle:bcprov-jdk15on:1.49'
  compile 'commons-httpclient:commons-httpclient:3.1'
  testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
      exclude module: "groovy-all"
  }
  testCompile 'org.java-websocket:Java-WebSocket:1.3.0'
}

// The Groovy that rest-assured drags in via ratpack-groovy-test is toxic, prevent it from coming in
// This should be fixed upstream in Ratpack
configurations.testCompile.dependencies.find { it.name == "ratpack-groovy-test" }.exclude(group: "org.codehaus.groovy")

apply plugin: "idea"

sourceCompatibility = "1.7"