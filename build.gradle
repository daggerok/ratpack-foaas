buildscript {
  repositories {
    maven { url "http://oss.jfrog.org/artifactory/repo" }
    jcenter()
  }
  dependencies {
    classpath 'io.ratpack:ratpack-gradle:1.4.0'
  }
}

repositories {
  jcenter()
  maven { url "https://oss.jfrog.org/repo" }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "idea"

sourceCompatibility = "1.8"

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

ext {
  authors = [
    'Dan Woods',
    'Luke Daley',
    'Eric Ridge',
    'Russell Hart'
  ]
}

println "Ratpack FOAAS - By: ${authors.join(', ')}"

dependencies {
  compile "com.itextpdf:itextpdf:5.4.2"
  runtime 'org.bouncycastle:bcprov-jdk15on:1.49'
  compile 'commons-httpclient:commons-httpclient:3.1'

  runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.2'
  runtime 'org.apache.logging.log4j:log4j-api:2.2'
  runtime 'org.apache.logging.log4j:log4j-core:2.2'
  runtime 'com.lmax:disruptor:3.3.0'

  testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
    exclude module: "groovy-all"
  }
  testCompile 'org.java-websocket:Java-WebSocket:1.3.0'
  testRuntime 'org.slf4j:slf4j-simple:1.7.7'
}

idea {
  project {
    jdkName "1.8"
    languageLevel "1.8"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        //configure git support for the project in idea
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
      }
    }
  }
}
